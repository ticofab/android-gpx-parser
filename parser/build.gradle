apply plugin: 'com.android.library'

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            maven(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'io.ticofab.androidgpxparser'
                artifactId = 'parser'
                version = '2.0.0'
            }
        }
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 10
        versionName "2.0.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    // JodaTime for Android
    // https://github.com/dlew/joda-time-android
    implementation 'net.danlew:android.joda:2.10.9.1'

    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
}
